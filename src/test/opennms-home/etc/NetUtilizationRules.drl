package org.opennms.netmgt.correlation.drools;

import java.util.Date;

import org.opennms.core.utils.InetAddressUtils;
import org.opennms.netmgt.correlation.drools.DroolsCorrelationEngine;
import org.opennms.netmgt.xml.event.Event;
import org.opennms.netmgt.model.events.EventBuilder;
import org.opennms.netmgt.EventConstants;
import org.opennms.netmgt.capsd.EventUtils;

global org.opennms.netmgt.correlation.drools.DroolsCorrelationEngine engine;
global org.opennms.netmgt.correlation.drools.NodeService nodeService;
global java.lang.Integer NET_UTIL_HIGH_TRIGGER_COUNT;
global java.lang.Integer NET_UTIL_HIGH_TIME_WINDOW;


/*
 * 9)  (Forwarding) If you receive >10 times traps netUtilizationHigh
 * Then generate one single major alarm to be forwarded via SNMP to VizGems and email to nmsadmin@att.com.
 */

declare HighNetUtilSituation
	node : Long
	timerId : Integer
	occurrences : Integer
end

rule "initial netUtilizationHigh event received for node"
	salience 100
	when
		$e : Event( $uei : uei == "uei.opennms.org/vendor/TallMaple/TMS/traps/netUtilizationHigh", $nodeid : nodeid, $ipaddr : interface )
		not( HighNetUtilSituation( node == $nodeid ) )
	then
		retract( $e );
		HighNetUtilSituation situation = new HighNetUtilSituation();
		situation.setNode( $nodeid );
		situation.setTimerId( engine.setTimer( NET_UTIL_HIGH_TIME_WINDOW ) );
		situation.setOccurrences( 1 );
		insert( situation );
		println( "Found netUtilizationHigh event for node " + $nodeid +". Inserted new situation " + situation + " and retracted event." );
		println( "Note: NET_UTIL_HIGH_TRIGGER_COUNT is set to " + NET_UTIL_HIGH_TRIGGER_COUNT );
end

rule "subsequent but NON-triggering netUtilizationHigh event received for node"
	salience 100
	when
		$e : Event( $uei : uei == "uei.opennms.org/vendor/TallMaple/TMS/traps/netUtilizationHigh", $nodeid : nodeid, $ipaddr : interface )
		$situation : HighNetUtilSituation( occurrences < ( NET_UTIL_HIGH_TRIGGER_COUNT ), node == $nodeid )
	then
		println( "Found netUtilizationHigh event for active situation " + $situation + ". Retracting event. Incrementing occurrences on situation." );
		retract( $e );
		incrementOccurrences( $situation );
		update( $situation );
end

rule "subsequent TRIGGERING netUtilizationHigh event received for node"
	salience 1000
	when
		$e : Event( $uei : uei == "uei.opennms.org/vendor/TallMaple/TMS/traps/netUtilizationHigh", $nodeid : nodeid, $ipaddr : interface )
		$situation : HighNetUtilSituation( $occ : occurrences >= ( NET_UTIL_HIGH_TRIGGER_COUNT ), node == $nodeid )
	then
		println( "Final netUtilizationHigh event on situation " + $situation + " triggered aggregate event, retracting event and situation; sending escalation event" );
		retract( $e );
		retract( $situation );
		sendAggregateNetUtilHighEvent( engine, $nodeid, $ipaddr, NET_UTIL_HIGH_TRIGGER_COUNT, NET_UTIL_HIGH_TIME_WINDOW );
end

rule "retract expired netUtilizationHigh situations"
	when
		$situation : HighNetUtilSituation( $nodeid : node, $timerId : timerId, $occ : occurrences < ( NET_UTIL_HIGH_TRIGGER_COUNT ) )
		$expiration : TimerExpired( id == $timerId )
	then
		println( "Found expired netUtilizationHigh situation " + $situation + "; retracting situation and expiration." );
		retract( $situation );
		retract( $expiration );
end


function void incrementOccurrences( HighNetUtilSituation situation ) {
	Integer occ = situation.getOccurrences();
	occ++;
	situation.setOccurrences( occ );
}

function void sendAggregateNetUtilHighEvent( DroolsCorrelationEngine engine, Long nodeId, String ipAddr, Integer count, Integer timeWindow ) {
		Integer timeWindowMinutes = timeWindow / 60000;
        EventBuilder bldr = new EventBuilder("uei.opennms.org/vendor/TallMaple/TMS/correlation/aggregateNetUtilizationHigh", "Drools")
                .setNodeid(nodeId.intValue())
                .setInterface(InetAddressUtils.addr(ipAddr))
		.addParam("occurrences", count.toString())
		.addParam("timeWindow", timeWindowMinutes.toString());
        engine.sendEvent(bldr.getEvent());
}

function void println(Object msg) {
	System.out.println(new Date() + " : " + msg);
}
